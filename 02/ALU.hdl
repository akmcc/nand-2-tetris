// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
   Mux16(a=x, b=false, sel=zx, out=l);
   Not16(in=l, out=notl);
   Mux16(a=l, b=notl, sel=nx, out=m);
   Mux16(a=y, b=false, sel=zy, out=n);
   Not16(in=n, out=notn);
   Mux16(a=n, b=notn, sel=ny, out=o);
   And16(a=m, b=o, out=p);
   Add16(a=m, b=o, out=q);
   Mux16(a=p, b=q, sel=f, out=r);
   Not16(in=r, out=notr);
   Mux16(a=r, b=notr, sel=no, out[15]=s, out[0..14]=t);
   Mux16(a=r, b=notr, sel=no, out[0..7]=aa, out[8..15]=bb);
   Or8Way(in=aa, out=xx);
   Or8Way(in=bb, out=zz);
   Or(a=xx, b=zz, out=yy);
   Not(in=yy, out=zr);
   Mux16(a=r, b=notr, sel=no, out=out);
   Mux(a=false, b=true, sel=s, out=ng);

}